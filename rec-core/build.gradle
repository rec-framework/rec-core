apply plugin: 'java'
apply plugin: 'jacoco'

repositories {
    mavenCentral()
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Rec v2',
                'Implementation-Version': version,
                'Main-Class': 'net.kimleo.rec.App'
    }
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

dependencies {
    compile project(":common")
    compile project(":rec-sepval")
    compile "org.mozilla:rhino:1.7.7.1"
    testCompile 'junit:junit:4.11'
    testCompile "org.mockito:mockito-core:2.+"
}


jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.83
            }
        }
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/scripting/**',
                              '**/lexer/*Token*',
                              '**/rec/App**'])
        })
    }
}

jacocoTestReport {
    reports {
        xml {
            enabled true
        }

        html {
            enabled true
        }
    }
}


check.dependsOn jacocoTestCoverageVerification